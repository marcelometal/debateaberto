jQuery ->
  $('body').prepend('<div id="fb-root"></div>')

  $.ajax
    url: "#{window.location.protocol}//connect.facebook.net/en_US/all.js"
    dataType: 'script'
    cache: true

  enviaFormulario = (callback) ->
    $this = $(this)
    $.ajax
      url: $this.attr('action') + '.json'
      data: $this.serialize()
      type: 'POST'
      dataType: 'json'
      success: (response) ->
        callback(response) if callback?
      error: (error) ->
        errorMsgs =
          'error': 'Ops, aconteceu um erro no servidor... Tente novamente mais tarde.'
        if error.status == 401 or error.status == 422
          errorMsgs = JSON.parse error.responseText
        $this.find('.modal-body').flashMsg errorMsgs

  window.autenticaUsuario = (data) ->
    data = JSON.parse data if typeof data == 'string'
    $('.modal').modal('hide')
    $('.painel').html JST['shared/usuario_dropdown'](data)

  $('#entrar form, #cadastrar form').submit ->
    enviaFormulario.call this, (response) ->
      window.autenticaUsuario response
    return false

  $('#recuperar_senha form').submit ->
    enviaFormulario.call this, (response) ->
      $('.modal').modal('hide')
      $('#recuperar_senha_ok').modal('show')
    return false

window.fbAsyncInit = ->
  FB.init
    appId: window.fbAppId
    cookie: true

  $('#entrar form .provedores a').click (e) ->
    e.preventDefault()
    FB.getLoginStatus (response) ->
      if response.authResponse
        $.getJSON('/usuarios/auth/facebook/callback', (usuario) ->
          window.autenticaUsuario usuario
        )
      else
        FB.login (response) ->
          if response.authResponse
            $.getJSON('/usuarios/auth/facebook/callback', (usuario) ->
              window.autenticaUsuario usuario
            )
